//当我们在组件模式开发时，业务组件应处于application属性，这时的业务组件就是一个 Android App，可以独立开发和调试；而当我们转换到集成模式开发时，业务组件应该处于 library 属性
if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    namespace 'com.xoliu.module_profile'
    compileSdk 33
    sourceSets {
        main {
            // 单独调试与集成调试时使用不同的 AndroidManifest.xml 文件
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }

    defaultConfig {
        if (isModule.toBoolean()) {
            // 单独调试时添加 applicationId ，集成调试时移除
            applicationId "com.xoliu.module_profile"
        }
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.viewpager1.runner.AndroidJUnitRunner"
        javaCompileOptions{
            annotationProcessorOptions{
                arguments = [AROUTER_MODULE_NAME:project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true    //开启ViewBinding
    }
}

dependencies {

    //图片选择器
    implementation 'com.github.gzu-liyujiang.AndroidPicker:ImagePicker:4.1.13'

    // PictureSelector 基础 (必须)
    implementation 'io.github.lucksiege:pictureselector:v3.0.9'

    // 图片压缩 (按需引入)
    implementation 'io.github.lucksiege:compress:v3.0.9'

    // 图片裁剪 (按需引入)
    implementation 'io.github.lucksiege:ucrop:v3.0.9'

    // 自定义相机 (按需引入)
    implementation 'io.github.lucksiege:camerax:v3.0.9'

    implementation project(':common')
    implementation project(':func_network')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    annotationProcessor 'com.alibaba:arouter-compiler:1.5.1'

}